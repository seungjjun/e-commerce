plugins {
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.hanghae'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

checkstyle {
    toolVersion = "8.42"
    configFile = file("${rootDir}/src/main/resources/checkstyles/naver-checkstyle-rules.xml")
    configProperties = ["suppressionFile": "${rootDir}/src/main/resources/checkstyles/naver-checkstyle-suppressions.xml"]
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

jacoco {
    toolVersion = "0.8.11"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation 'org.redisson:redisson-spring-boot-starter:3.29.0'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-cache'

    implementation 'org.springframework.kafka:spring-kafka'

    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.0'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'

    compileOnly 'org.projectlombok:lombok'

    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'

    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testImplementation "org.junit.jupiter:junit-jupiter:5.8.1"
    testImplementation "org.testcontainers:testcontainers:1.19.8"
    testImplementation "org.testcontainers:junit-jupiter:1.19.8"
    testImplementation "com.redis:testcontainers-redis:2.2.2"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()

    finalizedBy jacocoTestReport
    systemProperty 'spring.profiles.active', 'test'
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.required = true
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            element = 'CLASS'

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 1.0  // 100%
            }

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 1.0  // 100%
            }

            excludes = ['*.dto.*', '*.config.*', '*.storage.*', '*.api.eventlistener.*', '*.common.*', '*ECommerceApplication']
        }
    }
}
